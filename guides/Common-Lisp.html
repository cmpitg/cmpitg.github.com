= Quick notes on Common Lisp
:Author: Nguyễn Hà Dương (cmpitg)
:Email: <cmpitg@gmail.com>
:toc: left
:toclevels: 4
:numbered:
:source-highlighter: pygments
:pygments-css: class
:icons: font
:imagesdirs: ../assets/images

== Tips and tricks

=== Dumping core

For better startup time and ease of distribution, I usually load all necessary
libraries into a core, then dump it with `trivial-dump-core`.  The code would
look similar to this:

.dump-core.lisp
[source,lisp,linenums]
----
;;
;; Load Quicklisp
;;

#-quicklisp
(let ((quicklisp-init (merge-pathnames "quicklisp/setup.lisp"
                                       (user-homedir-pathname))))
  (when (probe-file quicklisp-init)
    (load quicklisp-init)))

(ql:quickload "quicklisp-slime-helper" :verbose nil :silent t)

;;
;; Load necessary packages
;;

(ql:quickload '("trivial-dump-core"
                "cl-fad"
                "trivial-utf-8"))

;;
;; And use them by default
;;

(use-package :cl-fad)
(use-package :trivial-dump-core)
(use-package :trivial-utf-8)

;;
;; Some user-defined functions
;;

(defun slurp (filespec &key (utf-8 t))
  "Reads and returns the whole file as string.  If `UTF-8' is `T' (default),
the file is read as UTF-8 string; otherwise, it's ANSI string."
  (if utf-8
      (with-open-file (in filespec :element-type '(unsigned-byte 8))
        (read-utf-8-string in :stop-at-eof t))
      (with-open-file (in filespec)
        (let ((seq (make-string (file-length in))))
          (read-sequence seq in)
          seq))))

;;
;; Now, dump it
;;

(dump-image "/tmp/batman.core")
----

To load the core for each working session, I create a script with content
similar to the following:

.sbcl-batman
[source,lisp,linenums]
----
#!/bin/sh

sbcl --noinform --core /tmp/batman.core "$@"
----

And voilà, everything is loaded and ready.

=== Suppressing banner

For SBCL: use `--noinform` argument when starting.

=== Joining strings

[source,lisp,linenums]
----
(let ((lines '("abc" "def" "ghi")))
  (format nil "~{~A ~}" lines)     ;; ⇨ "abc def ghi "
  (format nil "~{~A~^ ~}" lines)   ;; ⇨ "abc def ghi"
  (format nil "~{~A~^-~}" lines))  ;; ⇨ "abc-def-ghi"
----

=== Getting current working directory

* Avoid `*default-pathname-defaults*`, it's implementation-dependent.
* (SBCL) SBCL: `(sb-posix:getcwd)`.

[source,lisp,linenums]
----
;; Require just ASDF 3+
(uiop:getcwd)

;; Require cl-cwd
(cl-cwd:get-cwd)

;; Require osicat
(osicat:current-directory)
----

=== Changing current working directory/`cd`

[source,lisp,linenums]
----
;; Only in SBCL
(sb-posix:cwd "/tmp")

;; Require cl-cwd
(cl-cwd:cwd "/tmp")

;; Require cl-cwd, when you'd like to change it temporarily
(cl-cwd:with-cwd "/tmp/"
  (format t "~A" (directory (make-pathname :directory '(:relative ".")
                                           :name :wild
                                           :type :wild))))
----


=== Getting current file

`*load-truename*` or `*load-pathname*`, returning truename and pathname,
respectively.

=== Reading/slurping file

Avoid using `open` and `close` as 1) it's error-prone, and 2) the file might
not properly closed if you don't properly handle it.  Instead, use
`with-open-file`.

* Simplest way to read a line from a file:
+
[source,lisp,linenums]
----
(with-open-file (in #P"/tmp/tmp.txt")
  (format t "~A~%" (read-line in)))
----

* Reading binary:
+
[source,lisp,linenums]
----
(with-open-file (in #P"/tmp/tmp.txt" :element-type '(unsigned-byte 8))
  (read-byte in))
----


* Useful functions: `read-line`, `read-char`, `peek-char`, `file-position`
  (jumping to a position).

* To read whole file:

** The best way is probably to use `read-utf-8-string` from `trivial-utf-8`
   library:
+
[source,lisp,linenums]
----
(defun slurp (filespec)
  "Reads and returns the whole file as string."
  (with-open-file (in filespec :element-type '(unsigned-byte 8))
    (trivial-utf-8:read-utf-8-string in :stop-at-eof t)))
----

** Or if you don't care about encoding thing and just want to read ASCII, one
   way is to use `read-sequence` with `file-length` to read the content of the
   file into a string:
+
[source,lisp,linenums]
----
(with-open-file (in #P"/tmp/tmp.txt")
  (let ((seq (make-string (file-length in))))
    (read-sequence seq in)
    seq))
----

=== Merging paths

[source,lisp,linenums]
----
(merge-pathnames #P"lib/misc" #P"/home/batman")  ;; ⇨ #P"/home/lib/misc"
(merge-pathnames #P"lib/misc" #P"/home/batman/") ;; ⇨ #P"/home/batman/lib/misc"
(merge-pathnames #P"lib/misc" #P"home/batman")   ;; ⇨ #P"home/lib/misc"
(merge-pathnames #P"lib/misc" #P"home/batman/")  ;; ⇨ #P"home/batman/lib/misc"
----

=== Getting path to a system loaded with ASDF

[source,lisp,linenums]
----
(asdf:system-source-directory :optima) ;; ⇨ #P"/home/batman/quicklisp/dists/quicklisp/software/optima-20150302-git/"
----

=== Writing to file

* Write when file doesn't exist.  The following code will signal an error if
  `/tmp/tmp.txt` exists:
+
[source,lisp,linenums]
----
(with-open-file (out #P"/tmp/tmp.txt" :direction :output)
  (format t "Hello World~%"))
----

* Overwrite if file exists.  Note that `:if-exists` is `:supersude`, **not** `:overwrite`:
+
[source,lisp,linenums]
----
(with-open-file (out #P"/tmp/tmp.txt"
                     :direction :output
                     :if-exists :supersede)
  (format out "Xin chào thế giới~%"))
----
* Overwrite, starting from the beginning of the file.  Note that Note that `:if-exists` is `:overwrite`:
+
[source,lisp,linenums]
----
(with-open-file (out #P"/tmp/tmp.txt"
                     :direction :output
                     :if-exists :supersede)
  (format out "Hello world!~%"))

(with-open-file (out #P"/tmp/tmp.txt"
                     :direction :output
                     :if-exists :overwrite)
  (format out "Bye  "))

(with-open-file (in #P"/tmp/tmp.txt")
  (read-line in))
;;; ⇨ "Bye  world!
----
* Write binary:
+
[source,lisp,linenums]
----
(with-open-file (out #P"/tmp/abc.txt"
                     :direction :output
                     :element-type '(unsigned-byte 8))
  (write-byte 97 out)
  (write-byte 98 out)
  (write-byte 99 out))
;;; Write out "abc"
----

=== Listing directory

With `cl-pad` library:

[source,lisp,linenums]
----
(cl-pad:list-directory #P"/tmp/")                      ;; ⇨ List
(cl-pad:list-directory #P"/tmp/" :follow-symlinks t)   ;; ⇨ List
----

=== Checking if a file or directory exists

Either `probe-file` or `cl-fad`'s `file-exists` would work.

[source,lisp,linenums]
----
(probe-file #P"/home/batman")           ;; ⇨ #P"/home/batman/"
(cl-fad:file-exists-p #P"/home/batman") ;; ⇨ #P"/home/batman/"
(cl-fad:file-exists-p #P"/home/batma")  ;; ⇨ NIL
----

==== Checking directory exists only

With `cl-fad` library:

[source,lisp,linenums]
----
(cl-fad:directory-exists-p #P"/home/cmpitg") ;; ⇨ #P"/home/cmpitg/"
(cl-fad:directory-exists-p #P"/tmp/tmp.txt") ;; ⇨ NIL
----

=== Calculating MD5 of a string

With `ironclad` library:

[source,lisp,linenums]
----
(defun md5 (str)
  "Calculates MD5 hash of a string."
  (ironclad:byte-array-to-hex-string
   (ironclad:digest-sequence :md5 
                             (ironclad:ascii-string-to-byte-array str))))
----

=== Merging pathnames

With `cl-fad` library:

[source,lisp,linenums]
----
(path:catfile #P"/home/cmpitg" "tmp.txt")                    ;; ⇨ #P"/home/tmp.txt"
(path:catfile #P"/home/cmpitg/" "tmp.txt")                   ;; ⇨ #P"/home/cmpitg/tmp.txt"
(cl-fad:merge-pathnames-as-file #P"/home/cmpitg" "tmp.txt")  ;; ⇨ #P"/home/tmp.txt"
(cl-fad:merge-pathnames-as-file #P"/home/cmpitg/" "tmp.txt") ;; ⇨ #P"/home/cmpitg/tmp.txt"
----

=== `cd` to directory of current loading file

With `cl-cwd` and `cl-fad` libraries:

[source,lisp,linenums]
----
(use-package :cl-cwd)
(use-package :cl-fad)
(cwd (path:basename *load-truename*))
----

=== Using temporary file

``cl-fad``'s `open-temporary` opens a temporary file and returns the stream
corresponding to that file.  For conveniences, `with-output-to-temporary-file`
is often a better suit.

[source,lisp,linenums]
----
(cl-fad:with-output-to-temporary-file (out)
  (format out "Hello World"))
;; ⇨ #P"/tmp/temp-yccc29mc"

(with-open-file (in #P"/tmp/temp-yccc29mc")
  (read-line in))
;; ⇨ "Hello World"
----

=== Extracting dirname/parent directory

* Plain old Common Lisp:
+
[source,lisp,linenums]
----
(directory-namestring "/home/batman")  ;; ⇨ "/home/"
(directory-namestring "/home/batman/") ;; ⇨ "/home/batman/"
----

* With `path` package from `cl-fad` library:
+
[source,lisp,linenums]
----
(use-package :cl-fad)

(path:dirname #P"/home/batman")  ;; ⇨ #P"/home/"
(path:dirname #P"/home/batman/") ;; ⇨ #P"/home/batman/"
----

* Or with `cl-pad` directly:
+
[source,lisp,linenums]
----
(cl-fad:pathname-directory-pathname #P"/home/batman")            ;; ⇨ #P"/home/"
(cl-fad:pathname-directory-pathname #P"/home/batman/")           ;; ⇨ #P"/home/batman/"
(cl-fad:pathname-directory-pathname #P"/home/batman/hello.txt")  ;; ⇨ #P"/home/batman/"
----

=== Extracting basename/filename

* With `path` package from `cl-fad` library:
+
[source,lisp,linenums]
----
(path:basename #P"/home/batman")  ;; ⇨ #P"batman"
(path:basename #P"/home/batman/") ;; ⇨ #P""
----

* Or with plain old Common Lisp standard:
+
[source,lisp,linenums]
----
(file-namestring #P"/home/batman")  ;; ⇨ "batman"
(file-namestring #P"/home/batman/") ;; ⇨ ""
----

=== Comparing pathnames

With `cl-fad` library

[source,lisp,linenums]
----
(equalp #P"/home/batman" "/home/batman") ;; ⇨ NIL

(use-package :cl-fad)
(path:= #P"/home/batman" "/home/batman") ;; ⇨ T
----

=== Bitwise operations

[source,lisp,linenums]
----
(let ((a 7)
      (b 8))
  ;; AND
  (print (logand a b))                  ; ⇨ 0

  ;; OR = IOR = inclusive OR
  (print (logior a b))                  ; ⇨ 15

  ;; XOR
  (print (logxor a b))                  ; ⇨ 15

  ;; NOT
  (print (lognot a))                    ; ⇨ -8

  ;; Shift left
  (print (ash a b))                     ; ⇨ 1792

  ;; Shift right
  (print (ash a 2)))                    ; ⇨ 28
----

=== Slime - Starting swank server

[source,lisp,linenums]
----
(swank:create-server :port 4005 :style :spawn :dont-close t)
----

=== Slime - Interrupt/break infinite loop

`M-x slime-interrupt` or kbd:[C-c C-b] by default.

=== Updating Quicklisp systems and clients

[source,lisp,linenums]
----
(ql:update-client)
(ql:update-dist "package-name")
(ql:update-all-dists)
----

=== Add a local project to Quicklisp

Add your project to `~/quicklisp/local-projects/`

[source,sh,linenums]
----
cd ~/quicklisp/local-projects/
git clone <some-uri>
----

Then just `(ql:quickload "project-name")` as usual.

Another way is to push the directory containing `project-name.asd` into
`asdf:*central-registry*` then load with Quicklisp:

[source,lisp,linenums]
----
(push #p"/path/to/project/dir/" asdf:*central-registry*)
(ql:quickload "<project-name>")
----

=== Retrieving current ASDF version

[source,lisp,linenums]
----
(asdf:asdf-version)
----

=== Running external program

==== With ASDF's UIOP

[source,lisp,linenums]
----
;; uiop:run-program returns `output', `error', `return-value'

;; Just run it
(uiop:run-program "ls")

;; Output to `*standard-output*'
(uiop:run-program "ls" :output t)

;; Output as string
(uiop:run-program "ls" :output :string)
;; Extra ending newline stripped
(uiop:run-program "ls" :output '(:string :stripped t))

;; Feeding input
(uiop:run-program "cat" :input (make-string-input-stream "hello world"))

;; With shell
(uiop:run-program "ls / | grep tmp" :output '(:string :stripped t) :force-shell t)
----

See https://common-lisp.net/project/asdf/asdf/Some-Utility-Functions.html[ASDF
documentation] for more information.

== FAQs

=== What is truename?

From
http://www.lispworks.com/documentation/lw51/CLHS/Body/20_ac.htm[HyperSpec]:

[quote]
____
Truename is a pathname with any file system-level translations such as
resolving symbolic links performed.
____

=== Why should not string be used directly as _symbol name_

Try it yourself:

[source,lisp,linenums]
----
(find-symbol (string :*asdf-version*) :asdf)       ;; ⇨ ASDF/UPGRADE:*ASDF-VERSION*, :INHERITED
(find-symbol (symbol-name :*asdf-version*) :asdf)  ;; ⇨ ASDF/UPGRADE:*ASDF-VERSION*, :INHERITED
(find-symbol "*asdf-version*" :asdf)               ;; ⇨ NIL, NIL
(find-symbol "*ASDF-VERSION*" :asdf)               ;; ⇨ ASDF/UPGRADE:*ASDF-VERSION*, :INHERITED
(string :*asdf-version*)                           ;; ⇨ "*ASDF-VERSION*"
(symbol-name :*asdf-version*)                      ;; ⇨ "*ASDF-VERSION*"
----

Common Lisp doesn't guarantee a symbol name is an upper-case or lower-case
string across implementations.  So for portability reason, convert it with
`string`, or even better, use `symbol-name`.

=== ASDF quick start

Reference: https://common-lisp.net/project/asdf/asdf.html[ASDF manual]

* Basic operations:
+
[source,lisp,linenums]
----
;;; Load a system
(asdf:load-system :hunchentoot)
;; String works as well
(asdf:load-system "hunchentoot")
;; Or better, with Quicklisp
(ql:quickload "hunchentoot")

;;; Compile a system
(asdf:compile-system :foo-bar)

;;; Test a system
(asdf:test-system :foo-bar)

;;; Find system directory
(asdf:system-source-directory :hunchentoot)

;;; Find system definition file
(asdf:system-source-file :hunchentoot)
----

* Make sure ASDF find your system(s) when needed:
** put your system directories under `~/common-lisp/` or
   `~/.local/share/common-lisp/source/`; or
** use Quicklisp and put your system directories under
   `~/quicklisp/local-projects`; or
** Edit `~/.config/common-lisp/source-registry.conf` or add a `conf` file in
   `~/.config/common-lisp/source-registry.conf.d/` with the following content
+
[source,lisp,linenums]
----
(:tree "~/src/cl/")              ;; Find all systems in subdirectories of ~/src/cl, or
(:directory "~/src/cl/foo-bar")  ;; Just a specific location of one system
----
** Usually, ASDF will automatically read your configuration for the first time
   you try to load a system; however, you can force ASDF to re-read the
   configuration with `(asdf:clear-source-registry)`.

* For getting started developing Common Lisp with ASDF:


=== Difference between physical and logical pathname?

* From
  http://www.lispworks.com/documentation/lw51/CLHS/Body/t_logica.htm[HyperSpec]:
+
[quote]
____
(Logical pathname is) A pathname that uses a namestring syntax that is
implementation-independent, and that has component values that are
implementation-independent
____

* From http://www.gigamonkeys.com/book[Practical Common Lisp]:
+
[quote]
____
Logical pathnames allow Common Lisp programs to contain references to
pathnames without naming specific files. Logical pathnames could then be
mapped to specific locations in an actual file system when the program was
installed by defining a "logical pathname translation" that translates logical
pathnames matching certain wildcards to pathnames representing files in the
file system, so-called physical pathnames.
____

In modern system, it's safe to ignore the difference between logical and
physical pathnames.

=== CLISP failed to load Hunchentoot: `MAKE-PATHNAME` error

Find +hunchentoot/acceptor.lisp+ and change +"errors/"+ to +"errors"+.

=== The 2 namespaces: value cell and function cell

[source,lisp,linenums]
----
(defparameter *a* :foo-bar)
(defun *a* () nil)

(symbol-value '*a*)                            ;; ⇨ :FOO-BAR
(symbol-function '*a*)                         ;; ⇨ #<FUNCTION *A*>

(setf (symbol-value '*a*) 10)
*a*                                            ;; ⇨ 10
(*a*)                                          ;; ⇨ NIL
(setf (symbol-function '*a*) #'(lambda () t))
(*a*)                                          ;; ⇨ T

;;; Interestingly

(defmacro *a* () '(list 1))

(symbol-function '*a*)                         ;; ⇨ #<CLOSURE (LAMBDA (&REST SB-C::ARGS) :IN MACRO-FUNCTION) {1007F8018B}>
----

=== `symbol-function` vs. `fdefinition`

[source,lisp,linenums]
----
(defstruct person name age)

(fdefinition 'person-name)             ;; ⇨ #<FUNCTION PERSON-NAME>
(fdefinition '(setf person-name))      ;; ⇨ #<FUNCTION (SETF PERSON-NAME)>

(symbol-function 'person-name)         ;; ⇨ #<FUNCTION PERSON-NAME>
(symbol-function '(setf person-name))  ;; ⇨ Type error
----

According to Common Lisp HyperSpec:

* `symbol-function` takes a *symbol*, while
* `fdefinition` takes either a *symbol* or a `(setf symbol)` form that
  designates a *function name*

=== `typecase` vs. `etypecase` vs. `ctypecase`

Match type → perform action.

* `typecase`: if no match → returns `nil`.
* `ctypecase`: if no match → returns a _correctable error_.
* `etypecase`: if no match → returns a _non-correctable error_.

Examples:

[source,lisp,linenums]
----
(typecase "a" (string :string))         ; ⇨ :string
(typecase 1 (string :string))           ; ⇨ nil

(let ((x 1))
  (ctypecase x
    (string :string)
    (symbol (symbol-value x))))         ; ⇨ Asks to store value for X or abort

(let ((x 1))
  (etypecase x
    (string :string)
    (symbol (symbol-value x))))         ; ⇨ Error, cannot be corrected
----


== Useful libraries

* Pathnames and operating system interfaces: http://weitz.de/cl-fad[cl-pad],
  https://www.common-lisp.net/project/osicat/manual/osicat.html[cl-cwd], and
  https://github.com/Inaimathi/cl-cwd/[Osicat].

* Core dumping: https://github.com/rolpereira/trivial-dump-core[Trivial dump
  core].

* Regular expression: http://weitz.de/cl-ppcre/[cl-ppcre].

* Unicode utilities: http://weitz.de/cl-unicode/[cl-unicode] and
  https://common-lisp.net/project/trivial-utf-8/[Trivial-utf-8].

* HTTP client: http://weitz.de/drakma/[Drakma] for the original version, and
  https://github.com/orthecreedence/drakma-async[Drakma-async] for async version.

* HTTP server:
** http://weitz.de/hunchentoot/[Hunchentoot] is rock solid, feature-rich, and
   reliable.
** https://github.com/fukamachi/woo[Woo] is non-blocking, new, modern, and
   aims to be fastest web server ever written:
*** Built on top of http://software.schmorp.de/pkg/libev.html[libev];
*** Beats Node.js in most benchmarks and is comparable with Go;
*** Supports cluster.

* Data structure:
** https://github.com/Ramarren/ropes[Rope]

* Pattern matching: https://github.com/m2ym/optima[Optima].

* YAML parser: https://github.com/eudoxia0/cl-yaml[cl-yaml].

* Downloader: https://github.com/eudoxia0/trivial-download[trivial-download].

* SSH client library: https://github.com/eudoxia0/trivial-ssh[trivial-ssh],
  built on libssh2.

* Compress file extraction:
  https://github.com/eudoxia0/trivial-extract[trivial-extract].

* Password hashing and verification:
  https://github.com/eudoxia0/cl-pass[cl-pass].

* Fast HTTP parser: https://github.com/fukamachi/fast-http[fast-http].

* Developing graphics demos and games in real-time:
  https://github.com/cbaggers/cepl[Code-Eval-Play-Loop].

* HTML template library:
** http://weitz.de/cl-who/[cl-who] is thoughtful, mature, and sophisticated.

== Other interesting pieces

* https://github.com/robert-strandh/SICL[SICL] is an implementation written in
  such a way that the implementation is portable as much as possible.  Each
  SICL module could be integrated into other implementation with no or little
  change.

* https://github.com/google/lisp-koans[Lisp koans] helps learn Common Lisp
  more efficient.

* https://github.com:eudoxia0/asdf-linguist.git[asdf-linguist] makes ASDF
  useful as a build tool for languages other than Common Lisp.

* Common Lisp's killer features:

** Macro system makes powerful construct possible:
*** http://www.ai.sri.com/pkarp/loop.html[Loop macro].
*** https://common-lisp.net/project/iterate/[Interate] for cleaner and better
    looping.

** Powerful object system with CLOS:
*** http://cl-cookbook.sourceforge.net/clos-tutorial/[CLOS fundamentals].
